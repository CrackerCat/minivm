
func isprime
    r2 <- sint 3
@isprime.loop
    r0 <- smod r1 r2
    sbb r0 isprime.notprime isprime.next
@isprime.next
    r0 <- sint 2
    r2 <- sadd r2 r0
    r0 <- smul r2 r2
    sblt r1 r0 isprime.loop isprime.done 
@isprime.notprime
    r0 <- sint 0
    ret r0
@isprime.done
    r0 <- sint 1
    ret r0
end

func puti
    sbb r1 puti.ret puti.digit 
@puti.digit
    r0 <- sint 10
    r0 <- sdiv r1 r0
    r0 <- call puti r0
    r0 <- sint 10
    r1 <- smod r1 r0
    r0 <- sint 48
    r1 <- sadd r1 r0
    putchar r1
@puti.ret
    r0 <- sint 0
    ret r0
end

func primes
    r3 <- sint 2
    r0 <- sint 5
@primes.next
    r2 <- call isprime r0
    r3 <- sadd r3 r2
    r2 <- sint 2
    r0 <- sadd r0 r2
    sblt r0 r1 primes.ret primes.next
@primes.ret
    r3 <- call puti r3
    r3 <- sint 10
    putchar r3
    r3 <- sint 0
    ret r3
end

@main
    r0 <- sint 1000000
    r0 <- call primes r0
    exit